plugins {
    id 'java'
    id 'jacoco'    
    id 'application'
    id 'com.github.hierynomus.license' version '0.15.0'
}

mainClassName = 'vtlcli.VelocityCli'

repositories {
    jcenter()
}

dependencies {
    compile 'org.apache.velocity:velocity:1.7'
    compile 'info.picocli:picocli:3.6.1'
    compile 'org.slf4j:slf4j-api:1.7.26'
    compile 'org.slf4j:slf4j-simple:1.7.26'
    compile 'org.yaml:snakeyaml:1.24'

    testCompile 'junit:junit:4.12'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

check.dependsOn jacocoTestReport

license { 
    header = file('LICENSE_HEADER') 
    excludes(["**/*.yml"])
    mapping {
        java = 'SLASHSTAR_STYLE'
    }    
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': mainClassName,
                'Implementation-Version': project.version
    }
    from {
        configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
    destinationDir = project.buildDir
    archiveName = 'vtl-cli.jar'
}

build.dependsOn fatJar
